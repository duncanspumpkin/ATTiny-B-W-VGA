.include "tn45def.INC"

.equ BWHITE=2
.equ HSYNC=1
.equ VSYNC=0
.equ VGADDR=DDRB
.equ VGAPIN=PINB
.equ VGAPORT=PORTB
.equ HORIZONTALLINES = 636
.equ VERTICALLINES = 525 ;0x20d
.equ VERTICALSYNCPULSE = 2 ;0x002
;Vertical should start at 34 but since we are only outputing 20 pixels 
; of 5 times size 100 we start a bit later
.equ VERTICALACTIVESTART = 34 + (514-34-100)/2-1 ;0x022
.equ VERTICALACTIVEEND = 514 - (514-34-100)/2-1 ;0x202

.equ OUTPUTFRAME = SRAM_START ;This is 10 bytes long and address to what we are outputing

.def VERTLINENOL=r0 ;Permantly used for Vertical line no.
.def VERTLINENOH=r1

.def TILELINEL=r2
.def TILELINEH=r3
.def REGSTORE=r5
;Register r0-r12 are used by interrupt do not use.

.org 0
rjmp RESET

.org OC1Aaddr
; This is used to equalise interrupt latency
	sei ;1
	nop ;1 OVF1addr
	nop ;1 OVF0addr
	nop ;1 ERDYaddr
	nop	;1 ACIaddr
; Note do not use Timer 0/1 overflow, EEPROM ready and Analog Comparator interrupts
.org OC1Baddr
rjmp VIDEO

RESET:
	ldi r16,(1<<BWHITE)|(1<<HSYNC)|(1<<VSYNC)
	out VGADDR,r16

	ldi r16,low(RAMEND)
	out SPL,r16
	ldi r16,high(RAMEND)
	out SPH,r16

	; SET TIMER1 to fire interrupt on OCR1A match
	ldi r16,(1<<OCIE1A)|(1<<OCIE1B)
	out TIMSK,r16

	;Note that I am filling in OCR1C with values not OCR1A its
	;stupid but the only way this will work.

	; SET TIMER1 INTERRUPT TIME A VALUE
	ldi r16,(HORIZONTALLINES/4-1)
	out OCR1C,r16



	; SET TIMER1 TO SCLK WITH RESET COMPARING AGAINST OCR1C
	ldi r16,(1<<CS10)|(1<<CS11)|(1<<CTC1)
	out TCCR1,r16

	; Load in a basic frame
	ldi xl,low(OUTPUTFRAME)
	ldi xh,high(OUTPUTFRAME)
	ldi zl,low(2*TILE1)
	ldi zh,high(2*TILE1)
	st X+,ZL
	st X+,ZH	
	ldi zl,low(2*TILE2)
	ldi zh,high(2*TILE2)
	st X+,ZL
	st X+,ZH	
	ldi zl,low(2*TILE3)
	ldi zh,high(2*TILE3)
	st X+,ZL
	st X+,ZH	
	ldi zl,low(2*TILE4)
	ldi zh,high(2*TILE4)
	st X+,ZL
	st X+,ZH	
	ldi zl,low(2*TILE5)
	ldi zh,high(2*TILE5)
	st X+,ZL
	st X+,ZH
	
	;Setup Output stuff
	ser r16;
	mov r4,r16 ;1
	clr TILELINEL;1
	clr TILELINEH;1

	; Set TIME OCR1B we do this after a period of time so that the first
	; interrupt occurs properly
	ldi r16,2
	out OCR1B,r16	
	sei


	ldi r18,0
LOOPRESTART:	
	ldi r19,60
	sbrc r18,3
	 clr r18
LOOP:
	in r16,TCNT1
	ldi r17,0x1C
	cp r17,r16
	brlo LOOP
	ldi r16,0
	ldi r17,0
	cp VERTLINENOL,r16
	cpc VERTLINENOH,r17
	brne LOOP
	dec r19
	brne LOOP
	ldi xl,low(OUTPUTFRAME)
	ldi xh,high(OUTPUTFRAME)
	ldi r16,low(2*(TILE2-TILE1))
	ldi r17,high(2*(TILE2-TILE1))
	ldi ZL,low(2*(TILE1))
	ldi ZH,high(2*(TILE1))
	mov r13,r18
offsetmult:
	tst r13
	breq offsetmultend
	add ZL,r16
	adc ZH,r17
	dec r13
	rjmp offsetmult
offsetmultend:
	st X+,ZL
	st X+,ZH
	inc r18
	rjmp LOOPRESTART

; HORIZONTAL CLOCK TIMING
;HFP:12 (0-11)
;HSP:76 (12-87)
;HBP:36 (88-123)
;HPX:512 (124-635)
;TOT:636

; VERTICAL LINE TIMING
;VSP:2 (0-1)
;VBP:32 (2-33)
;VLN:480 (34-513)
;VFP:11 (514-524) 
;TOT:525 LINES
VIDEO:
;******************************
;* We have just left the HFP after 18 cycles now in HSP for 76 cycles
;******************************
	; Setup
	; Time  = 11 cycles
	cbi VGAPORT,HSYNC ;2
	;Save the status register
	pop REGSTORE;2
	pop REGSTORE ;2
	push r16 ;2
	push r19 ;2
	in REGSTORE,sreg ;1

	;Increment line
	; TIME = 3 cycles
	inc VERTLINENOL;1
	brne VERTADD ;2/1
	 inc VERTLINENOH;1
	VERTADD:
	
	;Reset Line count
	; Time = 11
	ldi r19,low(VERTICALLINES) ;1
	ldi r16,high(VERTICALLINES) ;1
	; Check to see if we need to start from the
	; begining.
	cp r19,VERTLINENOL ;1
	cpc r16,VERTLINENOH ;1
	breq restartVertCount ;1/2
	 nop ;-
	 nop ;-
	 nop ;3
	 rjmp noRestartVertCount ;2
	restartVertCount:
	 clr VERTLINENOL ;1
	 clr VERTLINENOH ;1
	 cbi VGAPORT,VSYNC ;2
	noRestartVertCount:
	clr r19 ;1

	; VERTICAL SYNC OFF AT LINE 2
	; TIME = 7 CYCLES
	ldi r16,low(VERTICALSYNCPULSE) ;1
	;ldi r17,high(VERTICALSYNCPULSE) ;1
	cp r16,VERTLINENOL ;1
	cpc r19,VERTLINENOH ;1
	breq s3 ;1/2
	 nop ;1
	 rjmp s4 ;2
	s3:
	sbi VGAPORT,VSYNC ;2
	s4:

	; Stop doing work if this is a blank
	; line.
	; ACTIVE PIXELS ON AT LINE 34
	; TIME = 6 CYCLES
	ldi r16,low(VERTICALACTIVESTART) ;1
	;ldi r17,high(VERTICALACTIVESTART) ;1
	cp r16,VERTLINENOL ;1
	cpc r19,VERTLINENOH ;1
	brlo activePixOn;2/1
	 rjmp activePixOnEnd ;2
activePixOn:
	set ;1
activePixOnEnd:

	; ACTIVE PIXELS OFF AT LINE 514
	; Time = 10 Cyclces
	ldi r16,low(VERTICALACTIVEEND) ;1
	ldi r19,high(VERTICALACTIVEEND) ;1
	cp r16,VERTLINENOL ;1
	cpc r19,VERTLINENOH ;1
	brlo activePixOff ;2/1
	 nop
	 nop
	 nop
	 nop
	 rjmp activePixOffEnd
activePixOff:
	ser r16	;1
	mov r4,r16 ;1
	clr TILELINEL;1
	clr TILELINEH;1
	clt ;1
activePixOffEnd:

 	; Fix timer
	; Time = 4 cycles
	clr r16 ;1
	out TCCR1,r16 ;1
	ldi r16,(1<<CS10)|(1<<CS11)|(1<<CTC1);1
	out TCCR1,r16 ;1
	; 49 Cycles	
	mov r9,XL ;1
	mov r10,XH;1
	mov r11,ZL;1
	mov r12,ZH;1
	nop
	nop
	nop
	nop
	nop
	nop
	nop
	nop
	nop
	nop
	nop
	nop
	nop
	nop
	nop
	nop
	nop 
	nop ;17
	sbi VGAPORT,HSYNC ;2
; ****************************************************************************************
; **** HORIZONTAL BACK PORCH = 36 CYCLES
; ****************************************************************************************
	brts dispvid;2/1
	rjmp novid ;2 Doesn't matter as this wont be run if there is vid
dispvid:
	;11 cycles
	inc r4 ;1
	ldi r16,0x5;1
	cp r4,r16 ;1
	brsh newLine;2/1
	nop ;1
	nop ;1
	nop ;1
	nop ;1
	rjmp noNewLine ;2
newLine:
	clr r4 ;1
	ldi r16,low((TILELINE3-TILELINE1));1 There is a good reason why this is 3
	ldi r19,high((TILELINE3-TILELINE1));1
	add TILELINEL,r16 ;1
	adc TILELINEH,r19 ;1

noNewLine:
	
	nop
	nop
	nop
	nop
	nop
	nop
	nop
	
	

	ldi XL,low(OUTPUTFRAME) ;1
	ldi XH,high(OUTPUTFRAME) ;1
	ld ZL,X+ ;2
	ld ZH,X+ ;2
	add ZL,TILELINEL ;1
	adc ZH,TILELINEH ;1
	lpm r8,Z+ ;3	
	ldi r16,5 ;1 There are 512 cycles we only output on 500 with 5 characters
	in r7,VGAPORT ;1
	bst r8,0 ;1
	bld r7,BWHITE ;1
; ****************************************************************************************
; **** HORIZONTAL ACTIVE LINE = 512 CYCLES / 4 = 128 PIXELS
; ****************************************************************************************	
vidOut:	
	out VGAPORT,r7 ;1 - 1	
	bst r8,1 ;1
	bld r7,BWHITE ;1
	bst r8,2 ;1
	nop ;1
	out VGAPORT,r7 ;1 - 2
	lpm r6,Z+ ;3
	bld r7,BWHITE ;1
	out VGAPORT,r7 ;1 - 3
	bst r8,3 ;1
	bld r7,BWHITE ;1
	nop ;1
	nop ;1
	out VGAPORT,r7 ;1 - 4
	bst r8,4 ;1
	bld r7,BWHITE ;1
	nop ;1
	nop ;1
	out VGAPORT,r7 ;1 - 5
	bst r8,5 ;1
	bld r7,BWHITE ;1
	nop ;1
	nop ;1
	out VGAPORT,r7 ;1 - 6
	bst r8,6 ;1
	bld r7,BWHITE ;1
	nop ;1
	nop ;1
	out VGAPORT,r7 ;1 - 7
	bst r8,7 ;1
	bld r7,BWHITE ;1
	mov r8,r6 ;1
	nop ;1
	out VGAPORT,r7 ;1 - 8
	bst r8,0 ;1
	bld r7,BWHITE ;1
	nop ;1
	nop ;1
	out VGAPORT,r7 ;1 - 9
	bst r8,1 ;1
	bld r7,BWHITE ;1
	nop ;1
	nop ;1
	out VGAPORT,r7 ;1 - 10
	bst r8,2 ;1
	bld r7,BWHITE ;1
	nop ;1
	nop ;1
	out VGAPORT,r7 ;1 - 11
	bst r8,3 ;1
	bld r7,BWHITE ;1
	bst r8,4 ;1
	nop ;1
	out VGAPORT,r7 ;1 - 12
	bld r7,BWHITE ;1
	lpm r6,Z+ ;3
	out VGAPORT,r7 ;1 - 13
	bst r8,5
	bld r7,BWHITE ;1
	ld ZL,X+ ;2
	out VGAPORT,r7 ;1 - 14
	bst r8,6 ;1
	bld r7,BWHITE ;1
	ld ZH,X+ ;2
	out VGAPORT,r7 ;1 - 15
	bst r8,7;1
	bld r7,BWHITE ;1
	mov r8,r6
	bst r8,0;1
	out VGAPORT,r7 ;1 - 16
	bld r7,BWHITE ;1
	bst r8,1
	add ZL,TILELINEL ;1
	adc ZH,TILELINEH ;1	
	out VGAPORT,r7 ;1 - 17
	bld r7,BWHITE ;1
	bst r8,2 ;1
	dec r16 ;1
	nop ;1
	out VGAPORT,r7 ;1 - 18
	bld r7,BWHITE ;1
	lpm r6,Z+ ;3
	out VGAPORT,r7 ;1 - 19
	bst r8,3 ;1
	bld r7,BWHITE ;1 
	mov r8,r6 ;1
	bst r8,0 ;1
	out VGAPORT,r7 ;1 - 20
	bld r7,BWHITE ;1
	breq noVid ;2/1
	rjmp vidOut ;2
novid:
	cbi VGAPORT,BWHITE ;2
	;Check that we have the final black band Should also reset output to off
	mov XL,r9 ;1
	mov XH,r10;1
	mov ZL,r11;1
	mov ZH,r12;1
	pop r19 ;2
	pop r16 ;2
	out sreg,REGSTORE ;1
	reti ;4

TILE1:
TILELINE1:
.db 0xFF, 0xFF, 0x0F, \
    0xFF, 0xFE, 0x0F
TILELINE3:    
.db 0xFF, 0xFC, 0x0F, \
    0xFF, 0xF8, 0x0F, \
    0xFF, 0xE0, 0x0F, \
    0xFF, 0xCC, 0x0F, \
    0xFF, 0xFC, 0x0F, \
    0xFF, 0xFC, 0x0F, \
    0xFF, 0xFC, 0x0F, \
    0xFF, 0xFC, 0x0F, \
    0xFF, 0xFC, 0x0F, \
    0xFF, 0xFC, 0x0F, \
    0xFF, 0xFC, 0x0F, \
    0xFF, 0xFC, 0x0F, \
    0xFF, 0xFC, 0x0F, \
    0xFF, 0xFC, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F
TILE2:
.db 0xFF, 0xFF, 0x0F, \
    0xFF, 0x3F, 0x0C, \
    0xFF, 0x0F, 0x00, \
    0xFF, 0xCF, 0x03, \
    0xEF, 0xEF, 0x07, \
    0xEF, 0xEF, 0x07, \
    0xFF, 0xEF, 0x0F, \
    0xFF, 0xCF, 0x0F, \
    0xFF, 0x9F, 0x0F, \
    0xFF, 0x3F, 0x0F, \
    0xFF, 0x7F, 0x0E, \
    0xFF, 0xFF, 0x0C, \
    0xFF, 0xFF, 0x09, \
    0xFF, 0xFF, 0x03, \
    0xEF, 0x0F, 0x00, \
    0xEF, 0x0F, 0x00, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F
TILE3:
.db 0xFF, 0xFF, 0x0F, \
    0xFF, 0xF8, 0x0F, \
    0xFF, 0xE0, 0x0F, \
    0xFF, 0xE7, 0x0F, \
    0x7F, 0xCF, 0x0F, \
    0x7F, 0xFF, 0x0F, \
    0x7F, 0xFF, 0x0F, \
    0xFF, 0xFC, 0x0F, \
    0xFF, 0xFC, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xCF, 0x0F, \
    0xFF, 0xC7, 0x0F, \
    0x7F, 0xE0, 0x0F, \
    0x7F, 0xF8, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F
TILE4:
.db 0xFF, 0xFF, 0x0F, \
    0xF7, 0x9F, 0x0F, \
    0xF3, 0x9F, 0x0F, \
    0xF9, 0x1F, 0x0F, \
    0xF9, 0x1F, 0x0E, \
    0xF9, 0x9F, 0x0C, \
    0xF1, 0x9F, 0x09, \
    0xF7, 0x9F, 0x0B, \
    0xF1, 0x9F, 0x03, \
    0xE8, 0x9F, 0x07, \
    0xEC, 0x0F, 0x00, \
    0xEC, 0x0F, 0x00, \
    0xFC, 0x9F, 0x0F, \
    0xF9, 0x9F, 0x0F, \
    0xF1, 0x9F, 0x0F, \
    0xF7, 0x9F, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F
TILE5:
.db 0xFF, 0xFF, 0x0F, \
    0xFF, 0xF0, 0x0F, \
    0xFF, 0xF0, 0x0F, \
    0xFF, 0xE7, 0x0F, \
    0xFF, 0xE7, 0x0F, \
    0xFF, 0xE7, 0x0F, \
    0xFF, 0xE0, 0x0F, \
    0xFF, 0xE0, 0x0F, \
    0xFF, 0xC7, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0x7F, 0xFF, 0x0F, \
    0x7F, 0xFF, 0x0F, \
    0xFF, 0xCF, 0x0F, \
    0xFF, 0xC7, 0x0F, \
    0xFF, 0xE0, 0x0F, \
    0xFF, 0xF0, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F
TILE6:
.db 0xFF, 0xFF, 0x0F, \
    0xF1, 0x3F, 0x0C, \
    0xF1, 0x0F, 0x08, \
    0xFF, 0xCF, 0x03, \
    0xEF, 0xEF, 0x07, \
    0xEF, 0xFF, 0x07, \
    0xE7, 0x3F, 0x04, \
    0xE1, 0x0F, 0x00, \
    0xE9, 0xCF, 0x03, \
    0xEC, 0xEF, 0x07, \
    0xEC, 0xEF, 0x07, \
    0xEC, 0xEF, 0x07, \
    0xEC, 0xEF, 0x07, \
    0xF9, 0xCF, 0x03, \
    0xF3, 0x0F, 0x00, \
    0xF7, 0x3F, 0x0C, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F
TILE7:
.db 0xFF, 0xFF, 0x0F, \
    0xFF, 0xE0, 0x07, \
    0xFF, 0xE0, 0x07, \
    0xFF, 0xFF, 0x0F, \
    0x7F, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFE, 0x0F, \
    0x7F, 0xFE, 0x0F, \
    0x7F, 0xFE, 0x0F, \
    0x7F, 0xFC, 0x0F, \
    0x7F, 0xFC, 0x0F, \
    0xFF, 0xF8, 0x0F, \
    0xFF, 0xF9, 0x0F, \
    0xFF, 0xF9, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F
TILE8:
.db 0xFF, 0xFF, 0x0F, \
    0xF0, 0x7F, 0x08, \
    0xE0, 0x3F, 0x00, \
    0xCC, 0x9F, 0x07, \
    0xCD, 0x9F, 0x0F, \
    0xC9, 0x9F, 0x0F, \
    0xE3, 0x9F, 0x07, \
    0xF3, 0x3F, 0x00, \
    0xE7, 0x3F, 0x00, \
    0xC7, 0x9F, 0x07, \
    0xCF, 0xCF, 0x0F, \
    0xCF, 0xCF, 0x0F, \
    0xCF, 0xCF, 0x0F, \
    0xCF, 0x9F, 0x07, \
    0xEF, 0x1F, 0x00, \
    0xFF, 0x7F, 0x00, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F
TILE9:
.db 0xFF, 0xFF, 0x0F, \
    0xFF, 0xF8, 0x0F, \
    0xFF, 0xE0, 0x0F, \
    0xFF, 0xE7, 0x0F, \
    0xFF, 0xCF, 0x0F, \
    0xFF, 0xCF, 0x0F, \
    0xFF, 0xCF, 0x0F, \
    0xFF, 0xCF, 0x0F, \
    0xFF, 0xC7, 0x0F, \
    0xFF, 0xE0, 0x0F, \
    0xFF, 0xF0, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xCF, 0x0F, \
    0xFF, 0xC7, 0x0F, \
    0xFF, 0xC0, 0x0F, \
    0xFF, 0xF0, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F
TILE10:
.db 0xFF, 0xFF, 0x0F, \
    0xF7, 0x3F, 0x0C, \
    0xF3, 0x1F, 0x00, \
    0xF9, 0x8F, 0x03, \
    0xEC, 0xCF, 0x07, \
    0xEC, 0xEF, 0x07, \
    0xEC, 0xEF, 0x07, \
    0xEC, 0xEF, 0x07, \
    0xE8, 0xEF, 0x07, \
    0xE0, 0xEF, 0x07, \
    0xEC, 0xEF, 0x07, \
    0xEC, 0xEF, 0x07, \
    0xE9, 0xCF, 0x07, \
    0xF1, 0x8F, 0x03, \
    0xF3, 0x1F, 0x00, \
    0xF7, 0x3F, 0x0C, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F
TILE11:
.db 0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xCF, 0x0F, \
    0x7F, 0xCF, 0x0F, \
    0x7F, 0xFF, 0x0F, \
    0x7F, 0xFF, 0x0F, \
    0x7F, 0xFF, 0x0F, \
    0x7F, 0xFF, 0x0F, \
    0x7F, 0xFF, 0x0F, \
    0x7F, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xCF, 0x0F, \
    0xFF, 0xCF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F, \
    0xFF, 0xFF, 0x0F

